personaje(pumkin, ladron(estacionesDeServicio)).
personaje(pumkin, ladron(licorerias)).
personaje(honeyBunny, ladron(estacionesDeServicio)).
personaje(vincent, mafioso(maton)).
personaje(jules, mafioso(maton)).
personaje(marsellus, mafioso(capo)).
personaje(winston, mafioso(resuelveProblemas)).
personaje(mia, actriz(1, drama)).
personaje(mia, actriz(2, musical)).
personaje(marilyn,actriz(10, musical)).
personaje(butch, boxeador).

pareja(marsellus, mia).
pareja(pumkin, honeyBunny).

%trabajaPara(Empleador, Empleado).
trabajaPara(marsellus, vincent).
trabajaPara(marsellus, jules).
trabajaPara(marsellus, winston).

amigo(vincent, jules).
amigo(jules, jimmie).
amigo(vincent, elVendedor).

sonAmigos(Persona1, Persona2):-
	amigo(Persona1, Persona2).
sonAmigos(Persona1, Persona2):-
	amigo(Persona2, Persona1).

sonPareja(Persona1, Persona2):-
	pareja(Persona1, Persona2).
sonPareja(Persona1, Persona2):-
	pareja(Persona2, Persona1).



%%%%%%%%%%Punto 1%%%%%%%%%%%%%%%%
	
esPeligroso(Personaje):-
	personaje(Personaje,Actividad),
	actividadPeligrosa(Actividad).
esPeligroso(Personaje):-
	trabajaPara(Personaje, Empleado),
	esPeligroso(Empleado).

actividadPeligrosa(mafioso(maton)).
actividadPeligrosa(ladron(licorerias)).

%%%%%%%%%Punto 2%%%%%%%%%%%%%%

duoTemible(CompincheOne, CompincheTwo):-
	esPeligroso(CompincheOne),
	esPeligroso(CompincheTwo),
	seRelacionan(CompincheOne, CompincheTwo).
seRelacionan(Persona1, Persona2):-
	sonAmigos(Persona1, Persona2).
seRelacionan(Persona1, Persona2):-
	sonPareja(Persona1, Persona2).
	
%%%%%%%%%%%Punto 3%%%%%%%%%%%%

esRespetable(Persona):-
	nivelDeRespeto(Persona, NivelDeRespeto),
	9 < NivelDeRespeto.
nivelDeRespeto(Persona, 10):-
	personaje(Persona, mafioso(resuelveProblemas)).
nivelDeRespeto(Persona, 1):-
	personaje(Persona, mafioso(maton)).
nivelDeRespeto(Persona, 20):-
	personaje(Persona, mafioso(capo)).

nivelDeRespeto(Persona, CoeficienteTotal):-
	personaje(Persona, actriz(_,_)),
	findall(Coeficiente, calculoDeCoeficiente(Persona, Coeficiente), Lista),
	sumlist(Lista, CoeficienteParcial),
	CoeficienteTotal is CoeficienteParcial / 3.
	
calculoDeCoeficiente(Persona, Coeficiente):-
	personaje(Persona, actriz(CantidadDePeliculas,Genero)),
	coeficientePorGenero(Genero, CoeficienteDelGenero),
	Coeficiente is CantidadDePeliculas * CoeficienteDelGenero.
coeficientePorGenero(drama, 5).
coeficientePorGenero(musical, 2).


%%%%%%%%%%%%%%%Punto 4%%%%%%%%%%%%%%%%%


encargo(marsellus, vincent, cuidar(mia)).
encargo(marsellus, vincent, buscar(mia,discoteca)).
encargo(marsellus, vincent, buscar(butch,losAngeles)).
encargo(vicent, elVendedor, cuidar(mia)).
encargo(marsellus, winston, ayudar(jules)).
encargo(marsellus, winston, ayudar(vincent)). 


estaEnProblemas(butch).
estaEnProblemas(Persona):-
	trabajaPara(Jefe, Persona),
	esPeligroso(Jefe),
	encargo(Jefe,Persona, cuidar(Pareja)),
	sonPareja(Jefe, Pareja).
estaEnProblemas(Persona):-
	encargo(_,Persona, buscar(Alguien,_)),
	esBoxeador(Alguien).
esBoxeador(Alguien):-
	personaje(Alguien, boxeador).
	

%%%%%%%%%%%%%%% Punto 5 %%%%%%%%%%%%

tieneCerca(Persona, Cercano):-
	sonAmigos(Persona, Cercano).
tieneCerca(Persona, Cercano):-
	trabajaPara(Persona, Cercano).
sanCayetano(Persona):-
	tieneCerca(Persona,_),
	forall(tieneCerca(Persona, Cercano), encargo(Persona, Cercano,_)).

%%%%%%%%%% Punto 6 %%%%%%%%%%%%%%%

masAtareado(Persona):-
	encargo(_,Persona,_),
	not(tieneMasEncargos(_, Persona)).

cantidadDeEncargos(Persona, Encargos):-
	encargo(_,Persona,_),
	findall(Encargo, encargo(_,Persona,Encargo), Lista),
	length(Lista, Encargos).

tieneMasEncargos(Persona, MenosAtareado):-
	cantidadDeEncargos(Persona, Encargos1),
	cantidadDeEncargos(MenosAtareado, Encargos2),
	Encargos1 > Encargos2.

%%%%%%%% Punto 7 %%%%%%%%%%%%%%%

hartoDe(Harto, Goma):-
	encargo(_,Harto,_),
	personaje(Goma,_),
	forall(encargo(_,Harto,Encargo),involucra(Encargo, Goma)).

involucra(buscar(Persona,_),Persona).
involucra(ayudar(Persona),Persona).
involucra(cuidar(Persona),Persona).